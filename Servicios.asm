;************************************************************************************

;						SERVICIOS DEL SISTEMA

;************************************************************************************



;--------------------------------------------------------------------------------------------

;											TEMPORIZADOR

;--------------------------------------------------------------------------------------------

;ASIGNA UN TEMPORIZADOR A LA TAREA ACTUALMENTE CORRIENDO

;VARIABLES USADAS
;------------------
;

;AFECTA:
;BANDERAS:

;NOTA:

TEMPORIZADOR_NB
	;DEFINIR QUE SE BUSCARA UN TEMP LIBRE
	BCF B_TEMPORIZADOR,TIPO_BUSQUEDA_TEMPORIZADOR
	;DEFINIR QUE LA BUSQUEDA NO SERA POR LLAVE
	BCF B_TEMPORIZADOR,TIPO_TEMPORIZADOR
	CALL BUSCAR_TEMPORIZADOR
	BSF B_TEMPORIZADOR,TIPO_TEMPORIZADOR
	GOTO SUB_ENTRADA_TEMPORIZADOR

TEMPORIZADOR
	;DEFINIR QUE SE BUSCARA UN TEMP LIBRE
	BCF B_TEMPORIZADOR,TIPO_BUSQUEDA_TEMPORIZADOR
	;DEFINIR QUE LA BUSQUEDA NO SERA POR LLAVE
	BCF B_TEMPORIZADOR,TIPO_TEMPORIZADOR
	CALL BUSCAR_TEMPORIZADOR
SUB_ENTRADA_TEMPORIZADOR
	BTFSC B_TEMPORIZADOR,TEMPORIZADOR_ENCONTRADO
	;SI, SE ENCONTRO UN TEMP LIBRE, ESCRIBIR EN EL BLOQUE LOS VALORES A TEMPORIZAR
	GOTO SELECCIONAR_TIPO_DE_TEMPORIZADOR
	GOTO PONER_EN_ESPERA_TEMP;NO, BLOQUEAR LA TAREA LLAMANTE

SELECCIONAR_TIPO_DE_TEMPORIZADOR
	BTFSS B_TEMPORIZADOR,TIPO_TEMPORIZADOR
	GOTO ESTABLECER_TEMP_B		;ESTABLECER UN TEMPORIZADOR BLOQUEANTE

	MOVF GENERAL_TEMP,W			;ESTABLECER UNO NO BLOQUEANTE, COPIAR LA LLAVE EN EL ESTADO DEL TEMP
	MOVWF INDF
	BSF INDF,7
	GOTO ESTABLECER_TEMP_NB

ESTABLECER_TEMP_B
	
	;COPIAR LA DIRECCION DEL ESTADO DE LA TAREA ACTUAL PRIMERO
	BANKSEL PRINCIPIO_TAREA_ANTERIOR;CAMBIAMOS DE BANCO PARA PODER ACCEDER A LA VARIABLE DIRECTAMENTE
	DECF PRINCIPIO_TAREA_ANTERIOR,W	;TOMAR EL PUNTERO A EL ESTADO DE LA TAREA LLAMANTE
									;COMO SE ESTA APUNTANDO AL PCH RESTANDO 1, SE APUNTA AL ESTADO
	MOVWF INDF						;Y GUARDAMOS EL PUNTERO DE ESTADO DE LA TAREA EN EL ESTADO DEL
									;TEMPORIZADOR
ESTABLECER_TEMP_NB
	
	BANKSEL B_TEMPORIZADOR	;CAMBIAMOS AL BANCO DONDE ESTAN LAS VARIABLES DE TEMPORIZACION
	INCF FSR			;APUNTAR A LOS TICKS
	MOVF TICK_8BIT_TMP,W;PASAMOS EL VALOR DE LOS TICKS A ESTE TEMPORIZADOR
	MOVWF INDF
	INCF FSR			;APUNTAR A LOS SEGUNDOS
	MOVF TICK_16BIT_TMP,W;PASAMOS EL VALOR DE LOS SEGUNDOS A ESTE TEMPORIZADOR
	MOVWF INDF
	INCF FSR			;APUNTAR A LOS MINUTOS
	MOVF TICK_24BIT_TMP,W;PASAMOS EL VALOR DE LOS MINUTOS A ESTE TEMPORIZADOR
	MOVWF INDF
	INCF FSR			;APUNTAR A LAS HORAS
	MOVF TICK_32BIT_TMP,W;PASAMOS EL VALOR DE LAS HORAS A ESTE TEMPORIZADOR
	MOVWF INDF
	MOVF FSR,W

	BTFSC B_TEMPORIZADOR,TIPO_TEMPORIZADOR	;DECIDIR EL BLOQUE DE LA TAREA
	GOTO TEMPORIZADOR_SALIR_CON_INTS

	BANKSEL PRINCIPIO_TAREA_ANTERIOR;CAMBIAMOS DE BANCO PARA PODER ACCEDER A LA VARIABLE		
									;DE TAREAS
	DECF PRINCIPIO_TAREA_ANTERIOR,W	;TOMAR EL PUNTERO AL ESTADO DE LA TAREA LLAMANTE
									;COMO SE ESTA APUNTANDO AL PCH, RESTANDO 1 SE APUNTA AL ESTADO
 	MOVWF FSR						;DE LA TAREA LLAMANTE
	MOVLW DEMORADA	;LA TAREA SE BLOQUEARA HASTA QUE SE ACABE EL TIEMPO
	MOVWF INDF
TEMPORIZADOR_SALIR_CON_INTS
	BSF INTCON,TMR0IE
	RETURN


PONER_EN_ESPERA_TEMP
	;SI NO SE CONSIGUE UN BLOQUE DE TEMPORIZACION LIBRE, SE BLOQUEA LA TAREA HASTA QUE LIBERE
	;UN BLOQUE
	BANKSEL PRINCIPIO_TAREA_ANTERIOR;CAMBIAMOS DE BANCO PARA PODER ACCEDER A LA VARIABLE		

	DECF PRINCIPIO_TAREA_ANTERIOR,W	;TOMAR EL PUNTERO A EL ESTADO DE LA TAREA LLAMANTE
									;COMO SE ESTA APUNTANDO AL PCH RESTANDO 1, SE APUNTA AL ESTADO
 	MOVWF FSR		;APUNTO AL ESTADO DE LA TAREA LLAMANTE
	MOVLW ESPERANDO_POR_TEMPORIZADOR
	MOVWF INDF		;PONEMOS A LA TAREA A ESPERAR A QUE SE LIBERE UN TEMPORIZADOR
	RETURN







;--------------------------------------------------------------------------------------------

;											BUSCAR_TEMPORIZADOR

;--------------------------------------------------------------------------------------------

;BUSCA EN EL BLOQUE DE TEMPORIZACION UN TEMPORIZADOR LIBRE, Y DEJA EL REGISTRO FSR APUNTANDO
;AL PRINCIPIO DEL BLOQUE DE TEMPORIZACION ENCONTRADO Y AL FINAL DEL SUPER BLOQUE DE TEMPORIZACION
;EN CASO DE NO ENCONTRAR NADA

;REGISTROS DE FUNCION SPECIALES UTILIZADOS
;------------------------------------------

; INDF, FSR, STATUS


;ENTRADAS
;---------
;TEMPORIZADORES BLOQUEANTES
;BANDERA: TIPO_BUSQUEDA_TEMPORIZADOR "1" -> BUSCAR UN TEMPORIZADOR EN USO
;		  TIPO_BUSQUEDA_TEMPORIZADOR "0" -> BUSCAR UN TEMPORIZADOR LIBRE


;TEMPORIZADORES NO BLOQUEANTES
;BANDERA: TIPO_TEMPORIZADOR "1" -> BUSCAR POR LLAVE
;		  TIPO_TEMPORIZADOR "0" -> BUSQUEDA NORMAL SEGUN (TIPO_BUSQUEDA_TEMPORIZADOR)
; LLAVE: DEBE ESTAR ALMACENADA EN GENERAL_TEMP ANTES DE CORRER LA FUNCION PARA UN TEMP NO BLOQUEANTE


;VARIABLES USADAS
;------------------

;	GENERAL_TEMP		;EN GENERAL_TEMP SE ALMACENA LA LLAVE PARA EL TEMP NO BLOQUEANTE

;BANDERAS AFECTADAS; Z
;TEMPORIZADOR_ENCONTRADO -> "1" CUANDO CONSIGUE UN TEMPORIZADOR BAJO EL CRITERIO DE BUSQUEDA
							;PREESTABLECIDO, "0" CUANDO NO CONSIGA UN TEMP QUE CUMPLA EL CRITERIO


;NOTA:
; EL FSR SE MODIFICA Y APUNTARA DENTRO DEL RANGO DE LAS VARIABLES DE TEMPORIZACION.
; EL DIRECCIONAMIENTO DIRECTO E INDIRECTO ESTARA UBICADO EN EL BANCO DONDE SE ENCUENTRAN
; LOS BLOQUES DE TEMPORIZACION, PRECAUCION DEBE TOMARSE ANTES DE LLAMAR A ESTA FUNCION, SE DEBE
; GUARDAR PREVIAMENTE LOS VALORES DE FSR Y STATUS YA QUE ESTA FUNCION LOS MODIFICA

;CAMBIA EL VALOR DE: W

BUSCAR_SIGUIENTE_TEMPORIZADOR
	BANKSEL B_TEMPORIZADOR
	BANKISEL B_TEMPORIZADOR
	GOTO ESCANEAR_LOS_BLOQUES

BUSCAR_TEMPORIZADOR
	BANKSEL B_TEMPORIZADOR
	BANKISEL B_TEMPORIZADOR
	MOVLW INICIO_BLOQUE_TEMPORIZADOR
	MOVWF PUNTERO_TEMPORIZADORES
ESCANEAR_LOS_BLOQUES
;	INCF BLOQUE_TEMP_ESCANEADOS		;INDICAR QUE UN BLOQUE MAS SERA ESCANEADO
	MOVF PUNTERO_TEMPORIZADORES,W	;CHEQUEAR SI SE ESTA AL FIN DEL BLOQUE DE TEMPORIZACION
	XORLW FIN_BLOQUE_TEMPORIZADOR
	BTFSS STATUS,Z
	GOTO BUSCAR_DENTRO_BLOQUE_TEMP
	GOTO TEMPORIZADOR_NO_UBICADO

BUSCAR_DENTRO_BLOQUE_TEMP	
	MOVF PUNTERO_TEMPORIZADORES,W
	MOVWF FSR						;TOMAR EL VALOR DEL PUNTERO Y APUNTAR CON EL

	;YA ESTAMOS APUNTANDO EN UN BLOQUE DE TEMPORIZACION, INDF CONTRENDRÁ EL ESTADO DEL TEMPORIZADOR
	;AHORA SE PROCEDERA A BUSCAR UN TEMPORIZADOR SEGUN EL CRITERIO DE BUSQUEDA
	
	;CHEQUEAMOS PRIMERO SI SE VA A BUSCAR UN TEMPORIZADOR POR LLAVE O DE MANERA NORMAL
	BTFSS B_TEMPORIZADOR, TIPO_TEMPORIZADOR
	GOTO BUSQUEDA_NORMAL

BUSCAR_POR_LLAVE
	BTFSC STATUS,Z						;¿EL TEMP ESTA LIBRE?
	GOTO MOVER_AL_SIGUIENTE_BLOQUE_TEMP	;SI, BUSCAR UNO OCUPADO
	;NO, EL TEMP ESTÁ OCUPADO
	BTFSS INDF,TIPO_TEMPORIZADOR ;¿EL TEMPORIZADOR ES NO BLOQUEANTE?
	GOTO MOVER_AL_SIGUIENTE_BLOQUE_TEMP			;NO, BUSCAR EN OTRO BLOQUE
	BCF INDF,TIPO_TEMPORIZADOR		;SI, APAGAR EL ULTIMO BIT PARA OBTENER LA LLAVE
	MOVF GENERAL_TEMP,W					;EN GENERAL_TEMP SE ENCUENTRA ALMACENADA LA LLAVE
	XORWF INDF,W						;Y LA COMPARAMOS CON LA ALMACENADA EN EL BLOQUE TEMP
	BSF INDF,TIPO_TEMPORIZADOR	;REESTABLECER EL ULTIMO BIT (PARA NO MODIFICAR NADA)
	BNZ MOVER_AL_SIGUIENTE_BLOQUE_TEMP	;¿ES LA LLAVE BUSCADA? (DEVOLVER BIT CAMBIADO)
	GOTO TEMPORIZADOR_UBICADO		;SE ENCONTRO LA CERRADURA COINCIDENTE CON LA LLAVE


	
BUSQUEDA_NORMAL
	MOVF INDF,W		;VALOR DEL ESTADO DEL TEMPORIZADOR
	BTFSC B_TEMPORIZADOR,TIPO_BUSQUEDA_TEMPORIZADOR	;¿BUSCAR LIBRE / OCUPADO? 
	GOTO BUSCAR_UNO_OCUPADO
BUSCAR_UNO_LIBRE
	BTFSC STATUS,Z				;¿ESTA LIBRE?
	GOTO TEMPORIZADOR_UBICADO	;SI, SE UBICO UN TEMP LIBRE
	GOTO MOVER_AL_SIGUIENTE_BLOQUE_TEMP
BUSCAR_UNO_OCUPADO
	BTFSS STATUS,Z				;¿ESTA OCUPADO?
	GOTO TEMPORIZADOR_UBICADO	;SI, SE UBICO UN TEMP OCUPADO
MOVER_AL_SIGUIENTE_BLOQUE_TEMP
	MOVLW .5
	ADDWF FSR,W						;APUNTAR AL SIGUIENTE BLOQUE
	MOVWF PUNTERO_TEMPORIZADORES
	GOTO ESCANEAR_LOS_BLOQUES			;BUSCAR EN BLOQUE SIGUIENTE

		
TEMPORIZADOR_UBICADO
	;SE ENCONTRO UN TEMPORIZADOR LIBRE
	BSF B_TEMPORIZADOR,TEMPORIZADOR_ENCONTRADO ;LEVANTAR LA BANDERA
	MOVLW .5
	ADDWF FSR,W
	MOVWF PUNTERO_TEMPORIZADORES
	GOTO SALIR_BUSCAR_TEMPORIZADOR		  ;SALIR
TEMPORIZADOR_NO_UBICADO
	;NO SE ENCONTRO UN TEMPORIZADOR LIBRE
	BCF B_TEMPORIZADOR,TEMPORIZADOR_ENCONTRADO ;LIMPIAR LA BANDERA
SALIR_BUSCAR_TEMPORIZADOR
	;SALIR
	RETURN



;--------------------------------------------------------------------------------------------

;									CHEQUEAR_TEMPORIZADOR

;--------------------------------------------------------------------------------------------

;CHEQUEA SI A UN TEMPORIZADOR NO BLOQUEANTE SE LE ACABO SU TIEMPO, DEVOLVIENDO EN EL ACUMULADOR
;UN VALOR DE RETORNO SEGUN EL CASO


;REGISTROS DE FUNCION SPECIALES UTILIZADOS
;------------------------------------------

; INDF, FSR, STATUS



;VARIABLES USADAS
;------------------

; GENERAL_TEMP = DEBE CONTENER EL VALOR DE LA LLAVE

;BANDERAS AFECTADAS; Z


;NOTA:

;CHEQUEAR_TEMPORIZADOR
CHEQUEAR_TEMPORIZADOR
	BSF B_TEMPORIZADOR,TIPO_TEMPORIZADOR
	MOVLW INICIO_BLOQUE_TEMPORIZADOR
	MOVWF PUNTERO_TEMPORIZADORES
	
	CALL BUSCAR_TEMPORIZADOR
	;VER LA RESPUESTA DE LA FUNCION BUSCAR_TEMPORIZADOR CHEQUEANDO LA BANDERA
	; B_TEMPORIZADORES <1> "TEMPORIZADOR ENCONTRADO"
	;ESTAMOS EN LA BUSQUEDA DE TEMPORIZADORES OCUPADOS PARA HACER UN DESCUENTO DE SUS VALORES
	
	;LA BANDERA (TEMPORIZADOR_ENCONTRADO) SE SETEARA SI SE CONSIGUE UN TEMPORIZADOR NO BLOQUEANTE
	BTFSC B_TEMPORIZADOR, TEMPORIZADOR_ENCONTRADO	
	GOTO $+2				;SE CONSIGUIO EL TEMPORIZADOR, CHEQUEAR SI SE TERMINO SU TIEMPO
	GOTO CT_NO_ENCONTRADO	;NO SE CONSIGUIO UN TEMP BAJO ESTA LLAVE
							;ESTO PUDO SER CAUSADO POR: 1) LA LLAVE NO FUE DEFINIDA
							;							2) EL TEMPORIZADOR YA FUE LIMPIADO

	CLRF GENERAL_TEMP		;USAREMOS A LA VARIABLE GENERAL_TEMP PARA CONTAR LAS VECES QUE
							;NOS HEMOS DESPLAZADO EN EL DIRECCIONAMIENTO INDIRECTO DE UN BLOQUE
							;DE TEMPORIZACION
							;ESTA VARIABLE SE USA PARA PASAR UN ARGUMENTO A LA 
							;FUNCION (BUSCAR_TEMPORIZADOR <ARGUMENTO LLAVE>)
							;PERO YA A ESTE PUNTO DE LA EJECUCCION PODEMOS UTILIZAR LA VARIABLE PARA
							;CONTAR LAS VECES QUE HA INCREMENTADO EL FSR
TEMP_NB_REVISAR_BLOQUE
	INCF FSR				;CHEQUEAR SI LOS TICKS, SEGUNDOS, MINUTOS, HORAS SON CERO
	INCF GENERAL_TEMP		;NOS HEMOS DESPLAZADO ASI QUE PARA NO PERDERNOS CONTAMOS
	MOVF INDF,W
	BNZ TEMP_NO_HA_CESADO	;TODAVIA HAY VALORES EN EL TEMPORIZADOR (NO HA TERMINADO DE CONTAR)
	MOVF GENERAL_TEMP,W		;¿YA SE LLEGÓ A EVALUAR A LAS HORAS?
	XORLW .4
	BNZ TEMP_NB_REVISAR_BLOQUE	;NO, TODAVIA NO SE HA LLEGADO A LAS HORAS, EVALUAR AL SIGUIENTE
	;SI, SE LLEGO A LAS HORAS Y TODAS LAS VARIABLES EVALUADAS SON CERO, POR LO TANTO LIBERAR
	;AL TEMPORIZADOR
	MOVLW 4
	SUBWF FSR,F				;APUNTAMOS AL ESTADO DEL TEMPORIZADOR
	CLRF INDF				;Y LIBERAMOS
TEMP_TERMINO
	RETLW TEMPORIZADOR_NB_EXPIRO	
TEMP_NO_HA_CESADO
	RETLW TEMPORIZADOR_NB_EN_CONTEO
CT_NO_ENCONTRADO
	RETLW TEMPORIZADOR_NB_NO_ENCONTRADO
