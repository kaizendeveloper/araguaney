;***********************************************************************************
;						SERVICIO DEL SISTEMA OPERATIVO
;***********************************************************************************

;EL SISTEMA OPERATIVO OFRECE LA CAPACIDAD DE TENER TEMPORIZADORES INDIVIDUALES CON LA CAPACIDAD
;DE CONTAR EL TIEMPO DESDE UN TICK DE RELOJ HASTA HORAS, CADA TEMPORIZADOR OCUPA 5 BYTES DE RAM
;LOS CUALES SE ACCEDEN DE MANERA INDIRECTA.

;						***** TICK DE RELOJ DEL SISTEMA *****



;*********************************
; RUTINA DE SERVICIO TEMPORIZADOR
;*********************************
	;******* DESHABILITAR INTS***********
	BANKSEL B_TEMPORIZADOR			;DIRECCIONAMIENTO DIRECTO
	BSF B_TEMPORIZADOR,TIPO_BUSQUEDA_TEMPORIZADOR	;BUSCAR TEMPS OCUPADOS
	BCF B_TEMPORIZADOR,TIPO_TEMPORIZADOR			;BUSQUEDA NORMAL

	MOVLW INICIO_BLOQUE_TEMPORIZADOR	;HACER TRAMPA PARA QUE EMPIEZE DESDE EL PRINCIPIO DEL BLOQUE
	MOVWF PUNTERO_TEMPORIZADORES
	
CHEQUEAR_TEMPORIZADORES
	CALL BUSCAR_SIGUIENTE_TEMPORIZADOR
	;VER LA RESPUESTA DE LA FUNCION BUSCAR_TEMPORIZADOR CHEQUEANDO LA BANDERA
	; B_TEMPORIZADORES <1> "TEMPORIZADOR ENCONTRADO"
	;ESTAMOS EN LA BUSQUEDA DE TEMPORIZADORES OCUPADOS PARA HACER UN DESCUENTO DE SUS VALORES

	;¿HAY UN TEMPORIZADOR EN USO?
	BTFSS B_TEMPORIZADOR,TEMPORIZADOR_ENCONTRADO
	GOTO SALIDA_TEMPORIZADORES
	INCF FSR,F
DEC_TICK_8BIT
	MOVF INDF,W	;ES TICK DE RELOJ CERO?
	BTFSC STATUS,Z	
	GOTO DEC_TICK_16_BIT	;SI, SE ACABARON LOS TICKS, DESCONTAR UN SEGUNDO
	DECF INDF,F			;NO, DESCONTAR UN TICK Y CHEQUEAR EL PROXIMO TEMPORIZADOR
	GOTO CHEQUEAR_TEMPORIZADORES
DEC_TICK_16_BIT
	INCF FSR	;MOVER EL PUNTERO DE RAM A LOS SEGUNDOS
	MOVF INDF,W	;OBTENER EL VALOR DE LA VARIABLE SEGUNDOS
	BTFSC STATUS,Z ;ES ESTA CERO?
	GOTO DEC_TICK_24_BIT ;SI, SE ACABARON LOS SEGUNDOS, DESCONTAR UN MINUTO
	DECF INDF	;NO, DESCONTAR UN SEGUNDO Y CHEQUEAR EL PROXIMO TEMPORIZADOR
	MOVLW .255  ;REPONER LA CANTIDAD DE TICKS NECESARIA PARA UN SEGUNDO
	DECF FSR	;MOVER EL PUNTERO DE RAM A LOS TICKS
	MOVWF INDF	;REPONER EL VALOR
	GOTO CHEQUEAR_TEMPORIZADORES
DEC_TICK_24_BIT
	INCF FSR	;MOVER EL PUNTERO DE RAM A LOS MINUTOS
	MOVF INDF,W ;LOS MINUTOS LLEGARON A CERO?
	BTFSC STATUS,Z 
	GOTO DEC_TICK_32_BIT	;SI, SE AGOTARON LOS MINUTOS, DESCONTAR UNA HORA
	DECF INDF	;NO, DESCONTAR UN MINUTO
	MOVLW .255	;REPONER LA CANTIDAD DE SEGUNDOS NECESARIA PARA UN MINUTO
	DECF FSR	;MOVER EL PUNTERO DE RAM A LOS SEGUNDOS
	MOVWF INDF	;YA DESCONTADO UN MINUTO, ESTABLECER LOS SEGUNDOS PARA SEGUIR EL CONTEO (LOGICO NO?)
	GOTO CHEQUEAR_TEMPORIZADORES
DEC_TICK_32_BIT
	INCF FSR	;MOVER EL PUNTERO DE RAM A LAS HORAS
	MOVF INDF,W	;LAS HORAS LLEGARON A CERO?
	BTFSC STATUS,Z
	GOTO APAGA_TEMPORIZADOR	;SI, SE LLEGO AL FIN DE TODO
	DECF INDF		;NO, DECREMENTAR UN HORA Y RESTABLECER LOS MINUTOS
	DECF FSR		;MOVER EL PUNTERO DE RAM A LOS MINUTOS
	MOVLW .255		;REPONER LA CANTIDAD DE MINUTOS NECESARIA PARA UNA HORA
	MOVWF INDF		;LISTO!
APAGA_TEMPORIZADOR	;APAGA EL TEMPORIZADOR
	MOVLW .4
	SUBWF FSR,W		;APUNTAR AL ESTADO DEL TEMPORIZADOR ACTUAL
	
	MOVWF GENERAL_TEMP_INT ;RESPALDAR EL FSR PARA ESTE BANCO
	MOVWF FSR

	BTFSC INDF,7		;VERIFICAR BIT 7 EN EL ESTADO DEL TEMPORIZADOR
	GOTO CHEQUEAR_TEMPORIZADORES

	MOVF INDF,W
	
	MOVWF FSR
	MOVLW ELEGIBLE
	MOVWF INDF
	BANKISEL B_TEMPORIZADOR
	MOVF GENERAL_TEMP_INT,W
	MOVWF FSR

	CLRF INDF		;Y LIBERARLO
	GOTO CHEQUEAR_TEMPORIZADORES

SALIDA_TEMPORIZADORES
	BCF INTCON,TMR0IF
	GOTO SALIDA_INTERRUPCION
